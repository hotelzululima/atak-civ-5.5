if (isJavaKernelBuild) {
    apply plugin: 'java-library' // apply the Java plugin

    java {
        sourceCompatibility = JavaVersion.VERSION_11
        targetCompatibility = JavaVersion.VERSION_11
    }

	tasks.withType(JavaCompile) {
		options.encoding = 'UTF-8'
	}

    sourceSets {
        main {
            // add default location for java specific sourceset
            java.srcDirs 'src/jre/java'
        }
        test {
            // add default location for
            java.srcDirs 'src/jniTest/java'
            // add default location for java specific sourceset
            java.srcDirs 'src/jreTest/java'

            // add testsupport sources
            java.srcDirs configProject.file('testsupport/src/jre/java')
            java.srcDirs configProject.file('testsupport/src/main/java')
        }
    }

    test {
        useJUnit()
        maxHeapSize = '1G'
    }

    task sourcesJar(type: Jar) {
        archiveClassifier.set('sources')
        from sourceSets.main.java.srcDirs
    }

    processResources {
        with copySpec {
            ['main', 'jre'].each {srcRoot ->
                file("src/${srcRoot}/res/").listFiles(new FileFilter() {
                    @Override
                    boolean accept(File pathname) {
                        return pathname.directory
                    }
                }).each { dir ->
                    from("src/${srcRoot}/res/${dir.name}")
                    {
                        into "resources/${dir.name}"
                        rename { String filename ->
                            if(filename.indexOf('.') > 0)
                            {
                                filename = filename.take(filename.lastIndexOf('.'))
                            }
                            return filename
                        }
                    }
                }
                file("src/${srcRoot}/assets/").listFiles(new FileFilter() {
                    @Override
                    boolean accept(File pathname) {
                        return pathname.directory
                    }
                }).each {dir ->
                    from("src/${srcRoot}/assets/${dir.name}")
                    {
                        into "assets/${dir.name}"
                    }
                }
                file("src/${srcRoot}/assets/").listFiles(new FileFilter() {
                    @Override
                    boolean accept(File pathname) {
                        return pathname.file
                    }
                }).each {f ->
                    from("src/${srcRoot}/assets/${f.name}")
                            {
                                into 'assets/'
                            }
                }
            }
        }
    }
    processTestResources {
        with copySpec {
            ['test', 'jniTest', 'jreTest'].each {srcRoot ->
                file("src/${srcRoot}/res/").listFiles(new FileFilter() {
                    @Override
                    boolean accept(File pathname) {
                        return pathname.directory
                    }
                }).each { dir ->
                    from("src/${srcRoot}/res/${dir.name}")
                    {
                        into "resources/${dir.name}"
                        rename { String filename ->
                            if(filename.indexOf('.') > 0)
                            {
                                filename = filename.take(filename.lastIndexOf('.'))
                            }
                            return filename
                        }
                    }
                }
                // asset subdirs
                file("src/${srcRoot}/assets/").listFiles(new FileFilter() {
                    @Override
                    boolean accept(File pathname) {
                        return pathname.directory
                    }
                }).each {dir ->
                    from("src/${srcRoot}/assets/${dir.name}")
                    {
                        into "assets/${dir.name}"
                    }
                }
                // asset files
                file("src/${srcRoot}/assets/").listFiles(new FileFilter() {
                    @Override
                    boolean accept(File pathname) {
                        return pathname.file
                    }
                }).each {f ->
                    from("src/${srcRoot}/assets/${f.name}")
                    {
                        into "assets"
                    }
                }
            }
        }
    }
} else if (isAndroidKernelBuild) {
    apply plugin: 'com.android.library' // apply the Android plugin

    // basic android library configuration
    android {
        compileSdkVersion project.androidCompileSdkVersion
        
        if(!namespace) {namespace "gov.tak.kernel.${project.name}"}

        buildTypes {
            debug {
                testCoverageEnabled (project.hasProperty('coverage'))
                if (project.hasProperty('coverage'))
                    android.defaultConfig.minSdkVersion 24
            }
            release {}
        }

        defaultConfig {
            targetSdkVersion project.androidTargetSdkVersion
            compileSdkVersion project.androidCompileSdkVersion
            minSdkVersion project.androidMinSdkVersion
            ndk {
                ndkVersion "${androidNdkVersion}"
                if (ndkAbiFilter != null)
                    abiFilters "${ndkAbiFilter}"
                else
                    abiFilters 'armeabi-v7a', 'arm64-v8a', 'x86', 'x86_64'
            }

            versionCode project.versionCode
            versionName "${project.version}"

            testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        }

        compileOptions {
            sourceCompatibility JavaVersion.VERSION_1_8
            targetCompatibility JavaVersion.VERSION_1_8
        }
        testOptions {
            unitTests.all {
                if(JavaVersion.current() != JavaVersion.VERSION_1_8){
                    jvmArgs '--illegal-access=warn'
                }
                jacoco {
                    //includeNoLocationClasses = true
                    excludes = ['jdk.internal.*']       //This line
                }
            }
            unitTests.returnDefaultValues = true
        }

        // create a default manifest
        def manifestFile = project.file('src/android/AndroidManifest.xml')

        if (!manifestFile.exists()) {
            manifestFile.getParentFile().mkdirs()
            manifestFile.text = """
<manifest xmlns:android="http://schemas.android.com/apk/res/android"
          package="gov.tak.kernel.${project.name}" >
</manifest>
"""
        }

        // note that `sourceSets` closure is _within_ the `android` closure for Android builds
        sourceSets {
            main {
                // specify the manifest
                manifest.srcFile 'src/android/AndroidManifest.xml'
                // add default location for android specific sourceset
                java.srcDirs 'src/android/java'
                // add default location for android JNI libs (shared across all build types)
                jniLibs.srcDirs 'src/android/jniLibs'
                // add default location for JNI sources (distinguished from C++ library sources)
                jni.srcDirs 'src/main/jni'
            }
            test {
                java.srcDirs 'src/main/test'
                java.srcDirs configProject.file('testsupport/src/main/java')
            }
            androidTest {
                // add default location for java specific sourceset
                java.srcDirs 'src/jniTest/java'
                res.srcDirs 'src/jniTest/res'
                assets.srcDirs 'src/jniTest/assets'

                // add testsupport sources
                manifest.srcFile configProject.file('testsupport/src/android/AndroidManifest.xml')
                java.srcDirs configProject.file('testsupport/src/android/java')
                java.srcDirs configProject.file('testsupport/src/main/java')
            }
        }
    }

    task sourcesJar(type: Jar) {
        archiveClassifier.set('sources')
        from android.sourceSets.main.java.srcDirs
    }
}

apply plugin: 'org.owasp.dependencycheck'
apply plugin: 'jacoco'

// Customize our archive basename, which defines the resulting publication artifactId
project.archivesBaseName = "${rootProject.name}-${project.name}"

dependencies {
    // Use JUnit test framework
    testImplementation("junit:junit:${junitVersion}")
    testImplementation "org.mockito:mockito-core:${mockitoVersion}"

    // apply dependencies for Android instrumented tests
    if(isAndroidKernelBuild) {
        androidTestImplementation 'androidx.test:runner:1.4.0'
        androidTestImplementation 'androidx.test:rules:1.4.0'
        androidTestImplementation 'androidx.test.uiautomator:uiautomator:2.2.0'
        androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
        androidTestImplementation 'androidx.test.espresso:espresso-intents:3.4.0'
        androidTestImplementation 'androidx.test.ext:junit:1.1.3'

        androidTestImplementation "org.mockito:mockito-android:3.12.4"
    } else if(isJavaKernelBuild) {
        testImplementation "gov.tak.support:android-port:${androidPortVersion}"
        testImplementation("org.slf4j:slf4j-simple:$slf4jApiVersion")
    }
}

jacoco {
    toolVersion = "$jacocoVersion"
}

// configure the build dir for the kernel build target (e.g. `build/android` or `build/java`)
project.buildDir = new File(buildDir, kernelBuildTarget)
