task createJacocoTestReport(type: JacocoReport) {
    if(isAndroidKernelBuild) {
        dependsOn('testDebugUnitTest', 'createDebugCoverageReport')
    } else {
        dependsOn('test')
    }

    reports {
        xml.required = true
        html.required = true
    }

    Set<String> executionDataPaths;
    if(isAndroidKernelBuild) {
        def fileFilter = ['**/R.class', '**/R$*.class', '**/BuildConfig.*', '**/Manifest*.*', '**/*Test*.*', 'android/**/*.*']
        def debugTree = fileTree(dir: "$project.buildDir/intermediates/javac/debug", excludes: fileFilter)

        sourceDirectories.setFrom(files(android.sourceSets.main.java.srcDirs))
        classDirectories.setFrom(files(debugTree))

        executionDataPaths = ['outputs/unit_test_code_coverage/debugUnitTest/testDebugUnitTest.exec',
                              'outputs/code_coverage/debugAndroidTest/connected/*.ec',
                              'outputs/code_coverage/debugAndroidTest/connected/**/*.ec']
    } else {
        sourceDirectories.setFrom(files(sourceSets.main.allSource.srcDirs))
        classDirectories.setFrom(files(sourceSets.main.output))

        executionDataPaths = ['jacoco/test.exec']
    }

    executionData.setFrom(fileTree(dir: project.buildDir, includes: executionDataPaths))

    description = "Generates unified code coverage report using Jacoco"
}

if(isJavaKernelBuild) {
    tasks.withType(Test) {
        jacoco.includeNoLocationClasses = true
        jacoco.excludes = ['jdk.internal.*']
    }
}
